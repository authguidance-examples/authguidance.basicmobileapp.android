apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    // Build with latest SDK, which is Android 9.0
    compileSdkVersion 28

    // Support devices from Android 5.0+, which use TLS 1.2
    defaultConfig {
        applicationId "authguidance.mobilesample"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        // This matches the redirect URI specified in res/raw/mobile_config.json
        manifestPlaceholders = [
                'appAuthRedirectScheme': 'com.mycompany.mobileapp'
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // This seems to be necessary after including kotlinx.coroutines for the async syntax
    // https://github.com/Kotlin/kotlinx.coroutines/issues/1064
    packagingOptions {
        pickFirst("META-INF/atomicfu.kotlin_module")
    }

    // Required in order to avoid okhttp3 expecting the minimum version to be 24
    // https://medium.com/square-corner-blog/okhttp-3-13-requires-android-5-818bb78d07ce
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def appauth_version = '0.7.1'
    def coroutines_version = '1.2.0'
    def okhttp3_version = "3.14.1"
    def gson_version = "2.8.5"
    def okio_version = "2.2.2"

    // Base libraries
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // Kotlin async support
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Import the AppAuth library to manage OAuth security and make it use the latest supportlib
    implementation("net.openid:appauth:$appauth_version") {
        exclude group: "com.android.support"
    }

    // A library for API requests
    implementation "com.squareup.okhttp3:okhttp:$okhttp3_version"

    // A library for JSON serialization
    implementation "com.google.code.gson:gson:$gson_version"

    // A library for file reading
    implementation "com.squareup.okio:okio:$okio_version"
    implementation 'com.android.support:support-v4:28.0.0'
}
