apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    // Build with latest SDK, which is Android Q
    compileSdkVersion 29

    // Support devices from Android 6.0+
    defaultConfig {
        applicationId "authguidance.mobilesample"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        // This matches the redirect URI specified in res/raw/mobile_config.json
        manifestPlaceholders = [
                'appAuthRedirectScheme': 'com.mycompany.mobileapp'
        ]
    }

    // For demo purposes we include the keystore with the app
    signingConfigs {
        debugAndRelease {
            storeFile file("${rootDir}/app/keystore/sample-keystore.jks")
            storePassword "android"
            keyAlias "androidapp"
            keyPassword "android"
        }
    }

    // Sign both debug and release builds
    buildTypes {
        debug {
            signingConfig signingConfigs.debugAndRelease
            debuggable true
        }
        release {
            signingConfig signingConfigs.debugAndRelease
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Enable newer data binding
    dataBinding {
        enabled = true
    }

    // This seems to be necessary after including kotlinx.coroutines for the async syntax
    // https://github.com/Kotlin/kotlinx.coroutines/issues/1064
    packagingOptions {
        pickFirst("META-INF/atomicfu.kotlin_module")
    }

    // Required in order to avoid okhttp3 expecting the minimum version to be 24
    // https://medium.com/square-corner-blog/okhttp-3-13-requires-android-5-818bb78d07ce
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// Dependency versions
ext {
    nav_version = "2.0.0"
    coroutines_version = '1.2.2'
    appauth_version = '0.7.1'
    okhttp3_version = "3.14.2"
    gson_version = "2.8.5"
    okio_version = "2.3.0"
}

dependencies {
    // Base libraries
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Navigation for Single Activity Apps
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Kotlin async support
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Import the AppAuth library to manage OAuth security but do not import the support lib
    implementation("net.openid:appauth:$appauth_version") {
        exclude group: "com.android.support"
    }

    // Include Chrome Custom tabs for the login window
    implementation 'androidx.browser:browser:1.0.0'

    // API requests
    implementation "com.squareup.okhttp3:okhttp:$okhttp3_version"

    // JSON serialization
    implementation "com.google.code.gson:gson:$gson_version"

    // File reading
    implementation "com.squareup.okio:okio:$okio_version"
}
